import net.darkhax.curseforgegradle.TaskPublishCurseForge
plugins {
    id 'fabric-loom' version '1.5.+'
    id 'maven-publish'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.18'
    id 'com.github.breadmoirai.github-release' version '2.+'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'signing'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    mavenLocal()
    maven {
        name = 'MinecraftForge'
        url = 'https://maven.minecraftforge.net/'
    }
    maven { url "https://maven.shedaniel.me" }
    maven {
        name = 'TerraformersMC'
        url = 'https://maven.terraformersmc.com/'
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        url = "https://raw.githubusercontent.com/CrypticVerse/projectresources/master/maven"
    }
}
spotless {
    java {
        removeUnusedImports()
        importOrder('java', 'javax', '', 'com.bookkeepersmc', 'net.minecraft', 'net.crypticverse')
        trimTrailingWhitespace()
    }

}

loom {
    accessWidenerPath = file("src/main/resources/betterbiomes.accesswidener")
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=betterbiomes"

            runDir "build/datagen"
        }
    }
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
    desktop {
        java {
            srcDirs("src/desktop/java")
        }
    }
}
dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    include "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include(modImplementation "com.bookkeepersmc:Notebook-api-fabric-${minecraft_version}:${notebook_version}")

    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${project.minecraft_version}-${project.terrablender_version}"
    include "com.github.glitchfiend:TerraBlender-fabric:${project.minecraft_version}-${project.terrablender_version}"

    modImplementation "com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_wood_api_version}"
    include "com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_wood_api_version}"

    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:${project.rei_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"

    modApi ("me.shedaniel.cloth:cloth-config-fabric:13.0.121") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modApi "dev.architectury:architectury-fabric:11.0.9"
    // Uncomment the following line to enable the deprecated Fabric API modules.
    // These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

    // modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}"}
    }
    def desktop = sourceSets.getByName("desktop")
    from(desktop.output.classesDirs)
    from(desktop.output.resourcesDir)
    manifest.attributes["Main-Class"] = "net.crypticverse.desktop.LaunchWarn"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "BetterBiomes-Fabric"
            from components.java
        }
    }
    repositories {
        maven {
            url = System.getenv("MAVEN_URL").toString()
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "Qs2o8KiI"
    versionNumber = "fabric-${mod_version}"
    versionName = "Better Biomes Fabric ${mod_name}"
    versionType = "beta"
    uploadFile = remapJar
    gameVersions = ["1.20.4"]
    loaders = ["fabric", "quilt"]
    changelog = project.file("CHANGELOG.md").text
    dependencies {
        required.project "fabric-api"
        required.project "terrablender"
        required.project "notebook-api"
        optional.project "rei"
    }
}
task curseforge(type: TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(938715, remapJar)
    mainFile.releaseType = 'beta'
    mainFile.displayName = "Better Biomes Fabric ${mod_name}"
    mainFile.addGameVersion("1.20.4")
    mainFile.addModLoader("Quilt")
    mainFile.addJavaVersion("Java 17")
    mainFile.changelog = project.file("CHANGELOG.md").text
    mainFile.changelogType = 'markdown'
    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("terrablender-fabric")
    mainFile.addRequirement("notebook-api")
    mainFile.addOptional("roughly-enough-items")
}
githubRelease {
    owner = "CrypticVerse"
    repo = "BetterBiomes"
    tagName = "fabric-${mod_name}"
    releaseName = "Better Biomes Fabric ${mod_name}"
    targetCommitish = "2.x-1.20.4"
    draft = false
    releaseAssets tasks.remapJar
    body = project.file("CHANGELOG.md").text
    token System.getenv("GITHUB_TOKEN")
}
tasks.register("publishMods") {
    dependsOn tasks.modrinth
    dependsOn tasks.curseforge
    dependsOn tasks.githubRelease
}